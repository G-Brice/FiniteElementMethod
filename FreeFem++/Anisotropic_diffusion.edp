// Domain contour:
border Gamma1(t=-1,1) { x = t; y = -1; }
border Gamma2(t=-1,1) { x = 1; y = t; }
border Gamma3(t=1,-1) { x = t; y = 1; }
border Gamma4(t=1,-1) { x = -1; y = t; }

// To build the mesh:
mesh Th=buildmesh(Gamma1(30)+Gamma2(30)+Gamma3(30)+Gamma4(30));

// To plot & check the mesh:
plot(Th,wait=true);

// Dirichlet BC:
func psi=(x+1)*(y==-1)+(3+y)*(x==1)*(y!=-1)+(3+x)*(y==1)*(x!=1)+(y+1)*(x==-1)*(y!=1);

// Definition of the space of solutions:
fespace Vh(Th,P1);

Vh u, v, f=2000*(x>-0.2)*(x<0.2)*(y>-0.2)*(y<0.2);

// Coefficients of the diffusion matrix
real a = 200;
real b = -50;
real c = 200;
real d = 50;

// Solving the problem:
solve Diffusion(u,v) = int2d(Th)(a*dx(u)*dx(v) + b*dy(u)*dx(v) + c*dx(u)*dy(v) + d*dy(u)*dy(v)) - int2d(Th)(f*v)
  + on(Gamma1,u=psi)
  + on(Gamma2,u=psi)
  + on(Gamma3,u=psi)
  + on(Gamma4,u=psi);

// Plot of level lines:
plot(u,wait=true);

// Saving the data for 3D display:
{
ofstream ff("graph.txt");
for (int i = 0; i < Th.nt; i++)
  {
    for (int j = 0; j < 3; j++)
      ff << Th[i][j].x << " "<< Th[i][j].y << " " << u[][Vh(i,j)] << endl;
    ff << Th[i][0].x << " " << Th[i][0].y << " " << u[][Vh(i,0)] << "\n\n\n";
  }
}

/* In gnuplot, type the following to get the 3D graph:
> set palette rgbformulae 30,31,32
> splot "graph.txt" w l pal */
